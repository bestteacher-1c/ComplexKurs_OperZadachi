Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	РеализацияТоваровИУслугТовары.Номенклатура,
		|	РеализацияТоваровИУслугТовары.Количество
		|ИЗ
		|	Документ.РеализацияТоваровИУслуг.Товары КАК РеализацияТоваровИУслугТовары
		|ГДЕ
		|	РеализацияТоваровИУслугТовары.Ссылка = &Ссылка
		|	И РеализацияТоваровИУслугТовары.Номенклатура.Услуга = ЛОЖЬ";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл

		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = Выборка.Количество;

	КонецЦикла;

	МенеджерВТ = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "ВЫБРАТЬ
		|	ОстаткиТоваров.Номенклатура,
		|	ОстаткиТоваров.Количество
		|ПОМЕСТИТЬ ВТПередЗаписью
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров КАК ОстаткиТоваров
		|ГДЕ
		|	ОстаткиТоваров.Регистратор = &Регистратор";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	Движения.Записать();

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "ВЫБРАТЬ
		|	ОстаткиТоваров.Номенклатура,
		|	ОстаткиТоваров.Количество
		|ПОМЕСТИТЬ ВТПослеЗаписи
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров КАК ОстаткиТоваров
		|ГДЕ
		|	ОстаткиТоваров.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПередЗаписью.Номенклатура,
		|	-1 * ВТПередЗаписью.Количество
		|ИЗ
		|	ВТПередЗаписью КАК ВТПередЗаписью
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПослеЗаписи.Номенклатура,
		|	СУММА(ВТПослеЗаписи.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТСписокНоменклатуры
		|ИЗ
		|	ВТПослеЗаписи КАК ВТПослеЗаписи
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПослеЗаписи.Номенклатура
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВТПослеЗаписи.Количество) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура,
		|	ОстаткиТоваровОстатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(
		|			&Период,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТСписокНоменклатуры.Номенклатура
		|					ИЗ
		|						ВТСписокНоменклатуры КАК ВТСписокНоменклатуры)) КАК ОстаткиТоваровОстатки
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПередЗаписью";

	Период = ?(Режим = РежимПроведенияДокумента.Оперативный
	, Неопределено
	, Новый Граница(МоментВремени(), ВидГраницы.Включая));

	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);

	РезультатЗапроса = Запрос.Выполнить();

	МенеджерВТ.Закрыть();

	Если РезультатЗапроса.Пустой() = Ложь Тогда

		Отказ = Истина;

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Сообщить("Нет " + ВыборкаДетальныеЗаписи.Номенклатура + " Нехватка "
				+ ВыборкаДетальныеЗаписи.КоличествоОстаток);

		КонецЦикла;

		Возврат;
		
	КонецЕсли;

	Для Каждого ТекСтрокаТовары Из Товары Цикл

		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Контрагент = Контрагент;
		Движение.Склад = Склад;
		Движение.Менеджер = Менеджер;
		Движение.Количество = ТекСтрокаТовары.Количество;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Себестоимость = 0;

	КонецЦикла;

	Движение = Движения.Взаиморасчеты.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Сумма = Товары.Итог("Сумма");

	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;

КонецПроцедуры

Процедура ЗарезервироватьТовары() Экспорт

	Возврат;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеализацияТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТовары.Количество) КАК Количество
		|ИЗ
		|	Документ.РеализацияТоваровИУслуг.Товары КАК РеализацияТовары
		|ГДЕ
		|	РеализацияТовары.Ссылка = &Ссылка
		|	И РеализацияТовары.Номенклатура.Услуга = ЛОЖЬ
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТовары.Номенклатура";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл

		Движение = Движения.ТоварыВРезерве.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = Выборка.Количество;

	КонецЦикла;

	Движения.ТоварыВРезерве.Записать();

КонецПроцедуры
