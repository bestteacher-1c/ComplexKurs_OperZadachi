
&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	ТабДок = Новый ТабличныйДокумент;
	
	ЗаполнитьТабличныйДокумент(ТабДок);
	
	ТабДок.ОтображатьСетку     = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ТолькоПросмотр      = Истина;
	
	ТабДок.Показать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьТабличныйДокумент(ТабДок)
	
	// Устанавливаем шрифт по умолчанию для всего документа:
	ТабДок.Область().Шрифт = Новый Шрифт("Arial", 12);
	
	// Устанавливаем нужную ширину столбцов:
	ТабДок.Область( , 1, , 1).ШиринаКолонки = 5;
	ТабДок.Область( , 2, , 2).ШиринаКолонки = 15;
	ТабДок.Область( , 3, , 3).ШиринаКолонки = 35;
	ТабДок.Область( , 4, , 4).ШиринаКолонки = 25;
	
	// Выводим заголовок отчета:
	ТабДок.Область(2, , 3).ВысотаСтроки = 25;
	ТабДок.Область(2, 2, 2, 4).Объединить();
	ТабДок.Область(2, 2).Текст      = "Товары";
	ТабДок.Область(2, 2).Шрифт      = Новый Шрифт("Arial", 18);
	ТабДок.Область(2, 2).ЦветТекста = WebЦвета.ТемноСиний;
	ТабДок.Область(2, 2).ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	
	// Шапка таблицы:
	ТабДок.Область(4, 2).Текст = "№ п/п";
	ТабДок.Область(4, 3).Текст = "Товар";
	ТабДок.Область(4, 4).Текст = "Количество";
	ТабДок.Область(4, 2, 4, 4).Шрифт    = Новый Шрифт("Arial", 10, Истина);
	ТабДок.Область(4, 2, 4, 4).ЦветФона = WebЦвета.АкварельноСиний;
	СтандартнаяЛиния = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	ТабДок.Область(4, 2, 4, 4).ГраницаСлева  = СтандартнаяЛиния;
	ТабДок.Область(4, 2, 4, 4).ГраницаСправа = СтандартнаяЛиния;
	ТабДок.Область(4, 2, 4, 4).ГраницаСверху = СтандартнаяЛиния;
	ТабДок.Область(4, 2, 4, 4).ГраницаСнизу  = СтандартнаяЛиния;
	ТабДок.Область(4, 2, 4, 4).ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	
	// Собираем данные для таблицы отчета:
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоступлениеТовары.Номенклатура КАК Номенклатура,
	|		ПоступлениеТовары.Количество КАК Количество
	|	ИЗ
	|		Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТовары
	|	ГДЕ
	|		ПоступлениеТовары.Ссылка.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РеализацияТовары.Номенклатура,
	|		-РеализацияТовары.Количество
	|	ИЗ
	|		Документ.РеализацияТоваровИУслуг.Товары КАК РеализацияТовары
	|	ГДЕ
	|		РеализацияТовары.Ссылка.Проведен) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НомерПП = 1;
	
	НомерСтроки = 5;
	
	Пока Выборка.Следующий() Цикл
		
		// Выводим строку таблицы:
		ТабДок.Область(НомерСтроки, 2).Текст = НомерПП;
		ТабДок.Область(НомерСтроки, 3).Текст = Выборка.Номенклатура;
		ТабДок.Область(НомерСтроки, 4).Текст = Выборка.Количество;
		
		ТабДок.Область(НомерСтроки, 2, НомерСтроки, 4).ГраницаСлева  = СтандартнаяЛиния;
		ТабДок.Область(НомерСтроки, 2, НомерСтроки, 4).ГраницаСправа = СтандартнаяЛиния;
		ТабДок.Область(НомерСтроки, 2, НомерСтроки, 4).ГраницаСнизу  = СтандартнаяЛиния;
		
		ТабДок.Область(НомерСтроки, 2).ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
		
		НомерПП = НомерПП + 1;
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры
